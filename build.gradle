buildscript {
	ext {
		bootVer = '3.3.1'
		packagePrefix = 'org.springframework.boot:spring-boot'
	}
}

plugins {
	id 'java'
	id 'idea'
	id 'org.springframework.boot' version "${bootVer}"
}

repositories { mavenCentral(); google(); gradlePluginPortal() }

project.group = 'io.doe'
project.version = '0.0.1'
project.description = 'spring resource server'

java.sourceCompatibility = 21
java.targetCompatibility = 21

configurations.configureEach { c ->
	c.exclude([group: 'log4j'])
	c.exclude([group: 'joda-time'])
	c.exclude([group: 'org.apache.logging.log4j'])
	c.exclude([group: 'commons-logging', module: 'commons-logging'])
	c.resolutionStrategy { rs -> rs.preferProjectModules() }
}

dependencies {
	implementation platform("${packagePrefix}-dependencies:${bootVer}")

	implementation "${packagePrefix}-starter-actuator"
	implementation "${packagePrefix}-starter-aop"
	implementation "${packagePrefix}-starter-cache"
	implementation "${packagePrefix}-starter-data-jpa"
	implementation "${packagePrefix}-starter-oauth2-resource-server"
	implementation "${packagePrefix}-starter-validation"
	implementation "${packagePrefix}-starter-web"

	implementation 'org.springframework.security:spring-security-oauth2-jose:6.3.1'

	implementation 'com.h2database:h2:2.2.224'

	implementation 'org.codehaus.janino:janino:3.1.12'
	implementation 'org.owasp:security-logging-logback:1.1.7'
	implementation 'com.github.gavlyukovskiy:p6spy-spring-boot-starter:1.9.1'

	implementation 'org.apache.commons:commons-text:1.12.0'
	implementation 'com.navercorp.lucy:lucy-xss-servlet:2.0.1'

	implementation 'com.google.guava:guava:33.2.1-jre'
	implementation 'com.github.ben-manes.caffeine:caffeine:3.1.8'

	testImplementation "${packagePrefix}-starter-test"

	implementation 'org.projectlombok:lombok:1.18.32'
	annotationProcessor 'org.projectlombok:lombok:1.18.32'
	annotationProcessor "${packagePrefix}-configuration-processor:${bootVer}"
}

idea { module { downloadSources = true } }

compileJava {
	inputs.files(processResources)
	options.annotationProcessorPath = configurations.annotationProcessor
	options.compilerArgs.add('-parameters')
	options.compilerArgs.add('-AaddGenerationDate=true')
	options.compilerArgs.add('-Amapstruct.defaultComponentModel=spring')
	options.compilerArgs.add('-Amapstruct.suppressGeneratorTimestamp=true')
}

jar { enabled = false }

bootJar { archiveFileName = "${rootProject.name}.jar" }

wrapper { gradleVersion = '8.5' }

test { useJUnitPlatform(); maxParallelForks(3) }
